#!/usr/bin/perl
use strict;
use warnings;
use feature qw(fc say);
use autodie;
use Encode  qw(decode);
use POSIX   qw(strftime);
use Term::ReadLine;
use InvertedIndex;
use Query;


print 'Indexing';
my $start = time;

my $index = InvertedIndex->new;
open my $fh, '<:encoding(utf-8)', shift || 'training.txt';
while (<$fh>)
{
    chomp;
    $index->add_document($_, fc $_);
}

say strftime " took %M:%S" => gmtime(time - $start);


my $term = Term::ReadLine->new('InvertedIndex');
while (defined($_ = $term->readline('query: ')))
{
    eval
    {
        my $query = Query::parse(decode 'UTF-8' => $_);
        my $row   = $query->run($index)->listref;
        say $_, "\t", $index->get_document($_) for @$row;
        say scalar @$row, " results";
    };
    warn "Invalid query: $@\n" if $@;
}

print "\nCleaning up, this may take a bit...\n";
