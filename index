#!/usr/bin/perl
use strict;
use warnings;
use feature qw(say);
use autodie;
use Encode  qw(decode);
use POSIX   qw(strftime);
use Term::ReadLine;
use InvertedIndex;
use Query;


my $docs  = shift // 'training.txt';
my $stash = "$docs.stash";

say 'Building index...';
my $start = time;
my $index = InvertedIndex->new;

say "Trying to unstash $stash...";
unless ($index->unstash($stash))
{
    say "Indexing $docs...";
    open my $fh, '<:encoding(utf-8)', $docs;
    $index->index($_) while <$fh>;

    say 'Stashing..';
    $index->stash($stash);
}

say strftime 'Index built after %M:%S' => gmtime(time - $start);


my $term = Term::ReadLine->new('InvertedIndex');
while (defined($_ = $term->readline('query: ')))
{
    if (my $query = Query::parse(decode 'UTF-8' => $_))
    {
        my $row = $query->run($index)->listref;
        print $_, "\t", $index->get_document($_) for @$row;
        say scalar @$row, " results";
    }
    else
    {
        warn "Invalid query.\n";
    }
}

print "\nCleaning up, this may take a bit...\n";
